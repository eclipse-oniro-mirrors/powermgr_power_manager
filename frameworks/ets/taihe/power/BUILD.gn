# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/ets_frontend/ets2abc_config.gni")
import("//build/ohos.gni")
import("//build/ohos/taihe_idl/taihe.gni")
import("../../../../powermgr.gni")

copy_taihe_idl("copy_power_manager") {
  sources = [ "idl/ohos.power.taihe" ]
}

subsystem_name = "powermgr"
part_name = "power_manager"
taihe_generated_file_path = "$taihe_file_path/out/$subsystem_name/$part_name"

ohos_taihe("run_taihe") {
  taihe_generated_file_path = "$taihe_generated_file_path"
  deps = [ ":copy_power_manager" ]
  outputs = [
    "$taihe_generated_file_path/src/ohos.power.ani.cpp",
    "$taihe_generated_file_path/src/ohos.power.abi.c",
  ]
}

taihe_shared_library("power_manager_taihe_native") {
  branch_protector_ret = "pac_ret"
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  taihe_generated_file_path = "$taihe_generated_file_path"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  sources = get_target_outputs(":run_taihe")
  include_dirs = [ "./include" ]
  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]
  sources += [
    "src/ani_constructor.cpp",
    "src/ohos.power.impl.cpp",
  ]
  deps = [
    ":run_taihe",
    "${powermgr_inner_api}:powermgr_client",
    "${powermgr_utils_path}/appmgr:power_appmgr",
  ]
  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
  ]
  if (has_ability_runtime_part) {
    external_deps += [ "ability_runtime:app_manager" ]
  }
}

generate_static_abc("power_abc") {
  base_url = "$taihe_generated_file_path"
  files = [ "$taihe_generated_file_path/@ohos.power.ets" ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/power_abc.abc"
  dependencies = [ ":run_taihe" ]
}

generate_static_abc("power_test") {
  base_url = "$powermgr_framework/ets/taihe/power/test"
  files = [
    "$powermgr_framework/ets/taihe/power/test/test_main.ets",
    "$powermgr_framework/ets/taihe/power/test/test_power.ets"
  ]
  is_boot_abc = "True"
  device_dst_file = "/system/framework/power_test.abc"
}

ohos_prebuilt_etc("power_abc_etc") {
  source = "$target_out_dir/power_abc.abc"
  module_install_dir = "framework"
  part_name = "$part_name"
  subsystem_name = "$subsystem_name"
  deps = [
    ":power_abc",
  ]
}