# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/config/components/idl_tool/idl.gni")
import("../../powermgr.gni")

config("powermgr_ipc_config") {
  include_dirs = [
    "../native/include",
    "include",
    "${powermgr_inner_api}/native/include",
    "${powermgr_inner_api}/native/include/hibernate",
    "${powermgr_inner_api}/native/include/shutdown",
    "${powermgr_inner_api}/native/include/suspend",
    "${target_gen_dir}",
  ]
}

idl_gen_interface("powermgr_interface") {
  sources = [ "IPowerMgr.idl" ]

  log_domainid = "0xD002902"
  log_tag = "PowerSvc"
  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}

ohos_source_set("powermgr_proxy") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":powermgr_interface")
  sources = filter_include(output_values, [ "*_proxy.cpp" ])
  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]

  public_configs = [ ":powermgr_ipc_config" ]

  deps = [ ":powermgr_interface" ]

  external_deps = [
    "c_utils:utils",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_single",
  ]

  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}

ohos_source_set("powermgr_stub") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }
  output_values = get_target_outputs(":powermgr_interface")
  sources = filter_include(output_values, [ "*_stub.cpp" ])
  sources += [
    "src/power_mgr_async_reply.cpp",
    "src/power_mode_callback_stub.cpp",
    "src/power_runninglock_callback_stub.cpp",
    "src/power_state_callback_stub.cpp",
    "src/screen_off_pre_callback_stub.cpp",
    "src/sync_sleep_callback_stub.cpp",
  ]

  configs = [
    "${powermgr_utils_path}:utils_config",
    "${powermgr_utils_path}:coverage_flags",
  ]

  public_configs = [ ":powermgr_ipc_config" ]

  deps = [
    ":powermgr_interface",
    "${powermgr_inner_api}:powermgr_client",
  ]

  external_deps = [
    "c_utils:utils",
    "hicollie:libhicollie",
    "hilog:libhilog",
    "ipc:ipc_core",
    "ipc:ipc_single",
  ]

  subsystem_name = "powermgr"
  part_name = "${powermgr_part_name}"
}